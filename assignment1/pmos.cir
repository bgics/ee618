.title PMOS Characterization

.include models-180nm

vdd vdd 0 dc 1.8
vsd vdd d dc 0.2
vsg vdd g dc 0

mp d g vdd vdd CMOSP W=7u L=0.18u

.control

run

set color0 = white
set color1 = black
set color2 = red

let gm_probe_vsg = 0.9
let gout_probe_isd = 400u
let vth_probe_vsg = 0.7

let k = 0
let w = 7u
let l = 0.18u

* ------------------ Isd vs Vsg @ Vsd = 0.2V ------------------ *

alter vsd dc 0.2
dc vsg 0 1.8 0.01

let isd = -i(vsd)
let vsg = v(vdd) - v(g)

plot isd vs vsg

* ------------------ Isd vs Vsg @ Vsd = 0.6V ------------------ *

alter vsd dc 0.6
dc vsg 0 1.8 0.01

let isd = -i(vsd)
let vsg = v(vdd) - v(g)

plot isd vs vsg

* ------------------ Isd vs Vsg @ Vsd = 1V ------------------ *

alter vsd dc 1
dc vsg 0 1.8 0.01

let isd = -i(vsd)
let vsg = v(vdd) - v(g)

let sqrt_isd = sqrt(isd)
let sqrt_slope = deriv(sqrt_isd)

plot sqrt_isd vs vsg

meas dc vth_probe_sqrt_slope find sqrt_slope when vsg = vth_probe_vsg
meas dc vth_probe_sqrt_isd find sqrt_isd when vsg = vth_probe_vsg

let vth = vth_probe_vsg - (vth_probe_sqrt_isd / vth_probe_sqrt_slope)
print vth

plot isd vs vsg

let slope = deriv(isd)
meas dc gm find slope when vsg = gm_probe_vsg

let k = (gm * l) / (w * (gm_probe_vsg - vth))

* ------------------ Isd vs Vsd @ Vsg = 0.6V ------------------ *

alter vsg dc 0.6
dc vsd 0 1.8 0.01

let isd = -i(vsd)
let vsd = v(vdd) - v(d)

plot isd vs vsd

* ------------------ Isd vs Vsd @ Vsg = 0.8V ------------------ *

alter vsg dc 0.8
dc vsd 0 1.8 0.01

let isd = -i(vsd)
let vsd = v(vdd) - v(d)

plot isd vs vsd

* ------------------ Isd vs Vsd @ Vsg = 1V ------------------ *

alter vsg dc 1
dc vsd 0 1.8 0.01

let isd = -i(vsd)
let vsd = v(vdd) - v(d)

plot isd vs vsd

let slope = deriv(isd)

meas dc gout find slope when isd = gout_probe_isd
meas dc gout_probe_vsd find vsd when isd = gout_probe_isd

let rout = 1 / gout
print rout

let lambda = 1 / ((rout * gout_probe_isd) - gout_probe_vsd)
print lambda

let k = k / (1 + lambda)
print k

* ------------------ isd vs vsg @ vsd = 0.2V, 0.6V, 1V  ------------------ *

dc vsg 0 1.8 0.01 vsd 0.2 1 0.4

let isd = -i(vsd)
let vsg = v(vdd) - v(g)

plot isd vs vsg

* ------------------ isd vs vsd @ vsg = 0.6V, 0.8V, 1V  ------------------ *

dc vsd 0 1.8 0.01 vsg 0.6 1 0.2

let isd = -i(vsd)
let vsd = v(vdd) - v(d)

plot isd vs vsd

.endc

.end
