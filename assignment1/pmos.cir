.title PMOS Characterization

.include models-180nm

vdd vdd 0 dc 1.8
vsd vdd d dc 0.2
vsg vdd g dc 0

mp d g vdd vdd CMOSP W=7u L=0.18u

.control

run

set color0 = white
set color1 = black
set color2 = red

let gm_probe_vsg = 0.9
let gout_probe_isd = 400u

let vth = 0
let lambda = 0

let w = 7u
let l = 0.18u

* ------------------ Isd vs Vsg @ Vsd = 0.2V ------------------ *

alter vsd dc 0.2
dc vsg 0 1.8 0.01

let isd = -i(vsd)
let vsg = v(vdd) - v(g)

plot isd vs vsg

* ------------------ Isd vs Vsg @ Vsd = 0.6V ------------------ *

alter vsd dc 0.6
dc vsg 0 1.8 0.01

let isd = -i(vsd)
let vsg = v(vdd) - v(g)

plot isd vs vsg

* ------------------ Isd vs Vsd @ Vsg = 1V ------------------ *

alter vsg dc 1
dc vsd 0 1.8 0.01

let isd = -i(vsd)
let vsd = v(vdd) - v(d)

plot isd vs vsd

let slope = deriv(isd)

meas dc gout find slope when isd = gout_probe_isd
let rout = 1 / gout
print rout

meas dc lambda_probe_slope find slope when isd = gout_probe_isd
meas dc lambda_probe_isd find isd when isd = gout_probe_isd
meas dc lambda_probe_vsd find vsd when isd = gout_probe_isd

let lambda = lambda_probe_slope / (lambda_probe_isd - (lambda_probe_slope * lambda_probe_vsd))
print lambda

* ------------------ Isd vs Vsg @ Vsd = 1V ------------------ *

alter vsd dc 1
dc vsg 0 1.8 0.01

let isd = -i(vsd)
let vsg = v(vdd) - v(g)

let sqrt_isd = sqrt(isd)
let sqrt_slope = deriv(sqrt_isd)

meas dc sqrt_slope_probe_vsg max_at sqrt_slope

meas dc vth_probe_sqrt_slope find sqrt_slope when vsg = sqrt_slope_probe_vsg
meas dc vth_probe_sqrt_isd find sqrt_isd when vsg = sqrt_slope_probe_vsg
meas dc vth_probe_vsg find vsg when vsg = sqrt_slope_probe_vsg

let vth = vth_probe_vsg - (vth_probe_sqrt_isd / vth_probe_sqrt_slope)
print vth

plot isd vs vsg

let slope = deriv(isd)
meas dc gm find slope when vsg = gm_probe_vsg

let vsd = v(vdd) - v(d)
meas dc k_probe_sqrt_slope find sqrt_slope when vsg = sqrt_slope_probe_vsg
meas dc k_probe_vsd find vsd when vsg = sqrt_slope_probe_vsg

let k = (2 * l * (k_probe_sqrt_slope^2)) / (w * (1 + (lambda * k_probe_vsd)))
print k

* ------------------ Isd vs Vsd @ Vsg = 0.6V ------------------ *

alter vsg dc 0.6
dc vsd 0 1.8 0.01

let isd = -i(vsd)
let vsd = v(vdd) - v(d)

plot isd vs vsd

* ------------------ Isd vs Vsd @ Vsg = 0.8V ------------------ *

alter vsg dc 0.8
dc vsd 0 1.8 0.01

let isd = -i(vsd)
let vsd = v(vdd) - v(d)

plot isd vs vsd

.endc

.end
