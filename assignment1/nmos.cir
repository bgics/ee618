.title NMOS Characterization

.include models-180nm

vds d 0 dc 0.2
vgs g 0 dc 0

mn d g 0 0 CMOSN W=2u L=0.18u

.control

run

set color0 = white
set color1 = black
set color2 = red

let gm_probe_vgs = 0.9
let gout_probe_ids = 350u

let vth = 0
let lambda = 0

let w = 2u
let l = 0.18u

* ------------------ Ids vs Vgs @ Vds = 0.2V ------------------ *

alter vds dc 0.2
dc vgs 0 1.8 0.01

let ids = -i(vds)
let vgs = v(g)

plot ids vs vgs

* ------------------ Ids vs Vgs @ Vds = 0.6V ------------------ *

alter vds dc 0.6
dc vgs 0 1.8 0.01

let ids = -i(vds)
let vgs = v(g)

plot ids vs vgs

* ------------------ Ids vs Vds @ Vgs = 1V ------------------ *

alter vgs dc 1
dc vds 0 1.8 0.01

let ids = -i(vds)
let vds = v(d)

plot ids vs vds

let slope = deriv(ids)

meas dc gout find slope when ids = gout_probe_ids
let rout = 1 / gout
print rout

meas dc lambda_probe_slope find slope when ids = gout_probe_ids
meas dc lambda_probe_ids find ids when ids = gout_probe_ids
meas dc lambda_probe_vds find vds when ids = gout_probe_ids

let lambda = lambda_probe_slope / (lambda_probe_ids - (lambda_probe_slope * lambda_probe_vds))
print lambda

* ------------------ Ids vs Vgs @ Vds = 1V ------------------ *

alter vds dc 1
dc vgs 0 1.8 0.01

let ids = -i(vds)
let vgs = v(g)

let sqrt_ids = sqrt(ids)
let sqrt_slope = deriv(sqrt_ids)

meas dc sqrt_slope_probe_vgs max_at sqrt_slope

meas dc vth_probe_sqrt_slope find sqrt_slope when vgs = sqrt_slope_probe_vgs
meas dc vth_probe_sqrt_ids find sqrt_ids when vgs = sqrt_slope_probe_vgs
meas dc vth_probe_vgs find vgs when vgs = sqrt_slope_probe_vgs

let vth = vth_probe_vgs - (vth_probe_sqrt_ids / vth_probe_sqrt_slope)
print vth

plot ids vs vgs

let slope = deriv(ids)
meas dc gm find slope when vgs = gm_probe_vgs

let vds = v(d)
meas dc k_probe_sqrt_slope find sqrt_slope when vgs = sqrt_slope_probe_vgs
meas dc k_probe_vds find vds when vgs = sqrt_slope_probe_vgs

let k = (2 * l * (k_probe_sqrt_slope^2)) / (w * (1 + (lambda * k_probe_vds)))
print k

* ------------------ Ids vs Vds @ Vgs = 0.6V ------------------ *

alter vgs dc 0.6
dc vds 0 1.8 0.01

let ids = -i(vds)
let vds = v(d)

plot ids vs vds

* ------------------ Ids vs Vds @ Vgs = 0.8V ------------------ *

alter vgs dc 0.8
dc vds 0 1.8 0.01

let ids = -i(vds)
let vds = v(d)

plot ids vs vds

.endc

.end
